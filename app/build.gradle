apply plugin: 'com.android.application'

ext {
    v = [
            cofoja: '1.3.1.5',
    ]

    cofoja = files("libs/cofoja-${v.cofoja}.jar")
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "edu.ucsd.flappycow"
        minSdkVersion 33
        targetSdkVersion 33
        versionCode 2023
        versionName "3.4.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }

        unitTests.all {
            jvmArgs "-ea", "-javaagent:./libs/cofoja-1.3.1.5.jar"
        }
    }
    namespace 'edu.ucsd.flappycow'

    tasks.withType(JavaCompile).configureEach {
        // Whenever we run javac, tell cofoja about the location of the Android stub jar.
        // Note: extra_classpath is a CSE218 customization to cofoja, because we can't know
        // (in this block) gthe actual classpath...
        options.compilerArgs += [
                "-Acom.google.java.contract.extra_classpath=" + files(
                        "${android.sdkDirectory.path}/platforms/android-28/android.jar"
                ).getAsPath()
        ]
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.browser:browser:1.5.0'
    implementation 'com.google.android.gms:play-services-ads:22.0.0'

    // We need to run cofoja even on non-test sources.
    implementation cofoja
    annotationProcessor cofoja

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'org.robolectric:robolectric:4.9.2'

    // We need this to actually _run_ contracts, so only in tests.
    testImplementation 'org.ow2.asm:asm:8.0'
    testImplementation cofoja

    testAnnotationProcessor 'com.google.auto.service:auto-service:1.0-rc6'
    testAnnotationProcessor cofoja
}
